buildscript {
    repositories {
        google()
        jcenter()
        mavenCentral()
        maven { url "https://jitpack.io" }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.3.2'
        classpath files('libs/gradle-witness.jar')
    }
}

repositories {
    google()
    jcenter()
    mavenCentral()
    maven { url "https://jitpack.io" }
}

apply plugin: 'com.android.application'
apply plugin: 'witness' // for dependencyVerification
// Consider getting rid of witness altogether
// https://forum.f-droid.org/t/is-the-gradle-witness-plugin-allowed/2247/3

dependencies {
    /** Support libraries */
    // used in various locations for a lot of components
    implementation 'com.android.support:design:27.1.1'
    // Used to edit the Exif in BitmapUtil
    implementation 'com.android.support:exifinterface:27.1.1'
    // Have this here just in case
    implementation 'com.android.support:support-v4:27.1.1'
    // one use in SearchToolbar
    implementation 'com.android.support:appcompat-v7:27.1.1'
    // actually a dependency of -v14
    implementation 'com.android.support:preference-v7:27.1.1'
    // used to prevent a 25... version to be loaded (from wherever)
    implementation 'com.android.support:support-v13:27.1.1'
    // used to manage the users preferences
    implementation 'com.android.support:preference-v14:27.1.1'
    // Vector drawable support
    implementation 'com.android.support:support-vector-drawable:27.1.1'
    // Used in several places
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    // Used in situations where data changes should be displayed directly (e.G. Search)
    implementation 'android.arch.lifecycle:common-java8:1.1.1'
    // Used in situations where data changes should be displayed directly (e.G. Search)
    implementation 'android.arch.lifecycle:extensions:1.1.1'

    // plays video
    implementation 'com.google.android.exoplayer:exoplayer:r2.3.1' //2.9.6

    // used as JSON library TODO: check if java builtin would work as well
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.9.8'

    // QR Code scanner
    implementation 'com.google.zxing:core:3.2.1' //3.3.3
    implementation 'com.journeyapps:zxing-android-embedded:3.4.0' //3.6.0

    // display message count on the home screen icon
    implementation 'me.leolin:ShortcutBadger:1.1.19' //1.1.22@aar

    // used in the emoji selector for the tab selection
    implementation 'com.jpardogo.materialtabstrip:library:1.0.9'

    // does the zooming on photos / media
    implementation 'com.github.chrisbanes:PhotoView:2.1.4' //2.3.0

    // image loading and caching library
    implementation 'com.github.bumptech.glide:glide:4.5.0' //4.9.0
    annotationProcessor 'com.github.bumptech.glide:compiler:4.5.0' //4.9.0

    // crops the avatars to circles
    implementation 'com.makeramen:roundedimageview:2.1.2' //2.3.0

    // used only in the "Progress Wheel" in Share Activity
    implementation 'com.pnikosis:materialish-progress:1.7'

    // used in Group Select Avatar, should be unified with profile
    implementation 'com.soundcloud.android:android-crop:0.9.10@aar' //1.0.1

    // DEPRECATED! Used to slide in the half-camera
    implementation 'com.nineoldandroids:library:2.4.0'

    // Used to pick the time for inactivity
    implementation 'mobi.upod:time-duration-picker:1.1.3'

    // number of unread messages,
    // the one-letter circle for the contacts (when there is no avatar) and a white background
    implementation 'com.amulyakhare:com.amulyakhare.textdrawable:1.0.1'

    // brings future java streams api to SDK Version < 24
    implementation 'com.annimon:stream:1.1.8' //1.2.1

    // glues the current time segment text in the gallery to the top
    implementation 'com.codewaves.stickyheadergrid:stickyheadergrid:0.9.6' //0.9.7

    // for the zooming on photos / media
    implementation('com.davemorrissey.labs:subsampling-scale-image-view:3.6.0', { //3.10.0
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    implementation 'com.mapbox.mapboxsdk:mapbox-android-sdk:7.2.0'

    /** Testing libraries */
    implementation 'junit:junit:4.12'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.assertj:assertj-core:3.12.2'
    testImplementation 'org.mockito:mockito-core:2.25.1'
    testImplementation 'org.powermock:powermock-api-mockito2:2.0.0'
    testImplementation 'org.powermock:powermock-classloading-xstream:2.0.0'
    testImplementation 'org.powermock:powermock-module-junit4:2.0.0'
    testImplementation 'org.powermock:powermock-module-junit4-rule:2.0.0'
    androidTestImplementation('org.assertj:assertj-core:3.12.2', {
        exclude group: 'org.hamcrest', module: 'hamcrest-core'
    })
}

dependencyVerification {
    // used by the witness plugin to verify dependencies.
    // to update the list, call `./gradlew -q calculateChecksums` and paste the result here.
    verify = [
            'com.android.support:design:7225973f7ee03765008a9c2f17a40b154c6885169fef022276e811c926a2202c',
            'com.android.support:exifinterface:50fd0852cd075f9d3c35081cbc2525a9c9a152acb4b0e52d38f3f8597d8bdf50',
            'com.android.support:support-v4:4f41dfc3e89f2738e45c86264a85c0934d055ee8ebe2020e23c97f303b80a48b',
            'com.android.support:appcompat-v7:0c7808fbbc5838d831e32e3c0a6f84e1f2c981deb8f11e010650f2b57923a335',
            'com.android.support:preference-v7:a1798a826b4097d00e49280f412b21af08f9bf1179c2e3838dc339d9f843416d',
            'com.android.support:support-v13:ff403b1098e689e2cfb1d2e213feb0a735f14522adf36d28dccb840f09fbdcd8',
            'com.android.support:preference-v14:dc058932e6fd93bf57d23fcc2f22351c21e5125e513721e4b0b03374341ad400',
            'com.android.support:support-vector-drawable:1c0f421114cf4627cf208776d6eb4f76340c78b7e96fe6e12b3e6eb950caf1b9',
            'com.android.support.constraint:constraint-layout:27b4e5c0b80d3ff8b92f4c93b3b4d3ecf16c01589f4cdf70ca7cf64cb42d8122',
            'android.arch.lifecycle:common-java8:7078b5c8ccb94203df9cc2a463c69cf0021596e6cf966d78fbfd697aaafe0630',
            'android.arch.lifecycle:extensions:429426b2feec2245ffc5e75b3b5309bedb36159cf06dc71843ae43526ac289b6',
            'com.google.android.exoplayer:exoplayer:955085aa611a8f7cf6c61b88ae03d1a392f4ad94c9bfbc153f3dedb9ffb14718',
            'com.fasterxml.jackson.core:jackson-databind:2351c3eba73a545db9079f5d6d768347ad72666537362c8220fe3e950a55a864',
            'com.google.zxing:core:b4d82452e7a6bf6ec2698904b332431717ed8f9a850224f295aec89de80f2259',
            'com.journeyapps:zxing-android-embedded:2422d83c2c09a7b645f516c8458ececba6a7da47b94e40778d876facf495c660',
            'me.leolin:ShortcutBadger:c6e58a7c0460e48f24627ea089a77f645f863f0aa12515d7c4057b9b6ee86118',
            'com.jpardogo.materialtabstrip:library:c6ef812fba4f74be7dc4a905faa4c2908cba261a94c13d4f96d5e67e4aad4aaa',
            'com.github.chrisbanes:PhotoView:04cb397fcb3df0757c8aed6927ebdd247930b5c78ee9acc59cd07dccdaaf3460',
            'com.github.bumptech.glide:glide:997de7ac95be6c944d3b8cbe13de11307736ea45451c1b09a6cec7c328ead59f',
            'com.github.bumptech.glide:compiler:5ba6d2cbd0bba1abf0d31b941afdd5269dcc80d0b8f0fc1c7d7b183beeecaaa8',
            'com.makeramen:roundedimageview:02a1c43d51dd600775f5e0f43f698355183fba1626c0d8728c4a5e82fba7bf8d',
            'com.pnikosis:materialish-progress:da089a90d1dab61e9b50038c09081019398f81190d12b0b567ce94b83ef8cf93',
            'com.soundcloud.android:android-crop:ffd4b973cf6e97f7d64118a0dc088df50e9066fd5634fe6911dd0c0c5d346177',
            'com.nineoldandroids:library:68025a14e3e7673d6ad2f95e4b46d78d7d068343aa99256b686fe59de1b3163a',
            'mobi.upod:time-duration-picker:db469ce0f48dd96b892eac424ed76870e54bf00fe0a28cdcddfbe5f2a226a0e1',
            'com.amulyakhare:com.amulyakhare.textdrawable:54c92b5fba38cfd316a07e5a30528068f45ce8515a6890f1297df4c401af5dcb',
            'com.annimon:stream:5da6e2e3e0551d61a3ea7014f04312276549e3dd739cf637996e4cf43c5535b9',
            'com.codewaves.stickyheadergrid:stickyheadergrid:f91a3d814cb1915ae03ea0b5bd3bd059158313ff38b89b9abd88ef2367ba1c67',
            'com.davemorrissey.labs:subsampling-scale-image-view:550c5baa07e0bb4ff0a18b705e96d34436d22619248bd8c08c08c730b1f55cfe',
            'com.mapbox.mapboxsdk:mapbox-android-sdk:8dac5a0fbded898d2b458f5122ebfe1485a0d68f1f594fb9aa9b38ffef35539b',
    ]
}

android {
    compileSdkVersion 27
    buildToolsVersion "28.0.3"

    defaultConfig {
        applicationId "com.b44t.messenger"
        minSdkVersion 14
        targetSdkVersion 26

        versionCode 540
        versionName "0.200.0"

        vectorDrawables.useSupportLibrary = true

        // base name of the generated apk
        project.ext.set("archivesBaseName", "deltachat")

        buildConfigField "boolean", "DEV_BUILD", "false"
        buildConfigField "String", "MAP_ACCESS_TOKEN", '"pk.eyJ1IjoiZGVsdGFjaGF0IiwiYSI6ImNqc3c1aWczMzBjejY0M28wZmU0a3cwMzMifQ.ZPTH9dFJaav06RAu4rTYHw"'

        ndk {
            abiFilters "armeabi", "armeabi-v7a", "x86"
        }
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
            jniLibs.srcDirs = ['libs']
        }
    }

    signingConfigs {
        signingConfigs {
            debug {
                // add `DC_DEBUG_STORE_FILE=/path/to/debug.keystore` to `~/.gradle/gradle.properties`
                if (project.hasProperty("DC_DEBUG_STORE_FILE")) {
                    storeFile file(DC_DEBUG_STORE_FILE)
                }
            }
            release {
                // can be defined at `~/.gradle/gradle.properties` or at "Build/Generate signed APK"
                if (project.hasProperty("DC_RELEASE_STORE_FILE")) {
                    storeFile file(DC_RELEASE_STORE_FILE)
                    storePassword DC_RELEASE_STORE_PASSWORD
                    keyAlias DC_RELEASE_KEY_ALIAS
                    keyPassword DC_RELEASE_KEY_PASSWORD
                }
            }
        }
        release
    }

    buildTypes {
        debug {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    'proguard/proguard-appcompat-v7.pro',
                    'proguard/proguard-glide.pro',
                    'proguard/proguard-jackson.pro',
                    'proguard/proguard-retrofit.pro',
                    'proguard/proguard-retrolambda.pro',
                    'proguard/proguard-rounded-image-view.pro',
                    'proguard/proguard-shortcutbadger.pro',
                    'proguard/proguard.cfg'
            testProguardFiles 'proguard/proguard-automation.pro',
                    'proguard/proguard.cfg'
            applicationIdSuffix ".beta"
        }
        release {
            minifyEnabled true
            proguardFiles = buildTypes.debug.proguardFiles
            signingConfig signingConfigs.release
        }
    }

    flavorDimensions "none"
    productFlavors {
        fat {
            dimension "none"
        }
        gplay {
            dimension "none"
            applicationId "chat.delta"
        }
    }

    applicationVariants.all { variant ->
        outputs.all {
            outputFileName = outputFileName.replace(".apk", "-${variant.versionName}.apk")
        }
    }

    compileOptions {
        encoding = 'UTF-8'
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        disable 'GoogleAppIndexingWarning', 'MissingTranslation'
        abortOnError false
    }

    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'LICENSE'
        exclude 'NOTICE'
        exclude 'asm-license.txt'
        exclude 'META-INF/*'
        doNotStrip '*/mips/*.so'
        doNotStrip '*/mips64/*.so'
    }

    useLibrary 'org.apache.http.legacy'
}
